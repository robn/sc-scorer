#!/usr/bin/env perl

use 5.034;
use warnings;
use experimental qw(signatures);
use HTTP::Tiny;
use JSON::MaybeXS;
use Hash::Util qw(lock_hashref_recurse);
use Data::Dumper;

### scoring

# supercoach 2021
my %sc2021_model = (
  # batting
  runs_multiplier => 1,

  strike_rate_bonus_thresholds => [
    [160, 25],
    [150, 20],
    [140, 15],
    [130, 10],
    [120, 5 ],
  ],
  strike_rate_bonus_cutoff => 20,

  runs_bonus_thresholds => [
    [100, 20],
    [50,  10],
  ],

  balls_bonus_thresholds => [],


  # bowling
  wickets_multiplier => 20,

  wickets_bonus_multipler => [
    [3, 10],
  ],

  maidens_multipler => 15,

  dots_multipler => 1,

  extras_multiplier => -1,

  econ_rate_bonus_thresholds => [
    [4, 25],
    [5, 20],
    [6, 15],
    [7, 10],
    [8, 5 ],
  ],
  econ_rate_bonus_cutoff => 3,


  # fielding
  catches_multiplier => 10,
  runout_multiplier => 20,
  stumping_multiplier => 15,


  # first innings bonus
  first_innings_multiplier => 1,
);

# tests
my %test_model = (
  # batting
  runs_multiplier => 1,

  strike_rate_bonus_thresholds => [
    [100, 30],
    [ 90, 25],
    [ 80, 20],
    [ 70, 15],
    [ 60, 10],
    [ 50, 5 ],
  ],
  strike_rate_bonus_cutoff => 30,

  runs_bonus_thresholds => [
    [250, 50],
    [200, 40],
    [150, 30],
    [100, 20],
    [50,  10],
  ],

  balls_bonus_thresholds => [
    [500, 100],
    [450,  90],
    [400,  80],
    [350,  70],
    [300,  60],
    [250,  50],
    [200,  40],
    [150,  30],
    [100,  20],
    [ 50,  10],
  ],


  # bowling
  wickets_multiplier => 30,

  wickets_bonus_multipler => [
    [3, 10],
    [5, 20],
  ],

  maidens_multipler => 2,

  dots_multipler => 0,

  extras_multiplier => -1,

  econ_rate_bonus_thresholds => [
    [1, 25],
    [2, 20],
    [3, 15],
    [4, 10],
  ],
  econ_rate_bonus_cutoff => 5,


  # fielding
  catches_multiplier => 10,
  runout_multiplier => 20,
  stumping_multiplier => 15,


  # first innings bonus
  first_innings_multiplier => 1,
);

my $model = \%test_model;
lock_hashref_recurse($model);


my ($match_id) = @ARGV;
unless ($match_id) {
  die "usage: $0 <match-id>\n";
}

my $ua = HTTP::Tiny->new;

my $scorecard_data = do {
  my ($res) = $ua->get("https://hs-consumer-api.espncricinfo.com/v1/pages/match/scorecard?lang=en&seriesId=1&matchId=$match_id");
  unless ($res->{success}) {
    die "E: couldn't get scorecard: $res->{status} $res->{reason}\n";
  }
  my $data = decode_json($res->{content});
  lock_hashref_recurse($data);
};

my $over_data = do {
  my ($res) = $ua->get("https://hs-consumer-api.espncricinfo.com/v1/pages/match/overs/details?lang=en&seriesId=1&matchId=$match_id&mode=ALL");
  unless ($res->{success}) {
    die "E: couldn't get overs $res->{status} $res->{reason}\n";
  }
  my $data = decode_json($res->{content});
  lock_hashref_recurse($data);
};

my %players;

# batting
my @batting_cards = 
  map {
    my $inn = $_->{inningNumber};
    map {
      {
        inningNumber => $inn,
        %$_,
      }
    }
    $_->{inningBatsmen}->@*
  }
  $scorecard_data->{content}{scorecard}{innings}->@*;

for my $card (@batting_cards) {
  # initial player setup from batting card
  my $player = $players{$card->{player}{id}} //= {
    info  => $card->{player},
    score => 0,
  };

  next unless $card->{battedType} eq 'yes';

  my $runs_score        = $card->{runs} * $model->{runs_multiplier};
  my $strike_rate_bonus =
    $card->{runs} >= $model->{strike_rate_bonus_cutoff} ?
      threshold_bonus($card->{strikerate}, $model->{strike_rate_bonus_thresholds}) : 0;
  my $runs_bonus        = threshold_bonus($card->{runs}, $model->{runs_bonus_thresholds});
  my $balls_bonus       = threshold_bonus($card->{balls}, $model->{balls_bonus_thresholds});

  my $innings_multiplier = $card->{inningNumber} < 3 ? $model->{first_innings_multiplier} : 1;

  $player->{score} += ($runs_score + $strike_rate_bonus + $runs_bonus + $balls_bonus) * $innings_multiplier;

  say "BAT $card->{inningNumber} $player->{info}{name}: RS $runs_score SRB $strike_rate_bonus RB $runs_bonus";
}

say "";

# bowling
my @bowling_cards = 
  map {
    my $inn = $_->{inningNumber};
    map {
      {
        inningNumber => $inn,
        %$_,
      }
    }
    $_->{inningBowlers}->@*
  }
  $scorecard_data->{content}{scorecard}{innings}->@*;

for my $card (@bowling_cards) {
  # initial player setup from bowling card
  # again? yes. what if subs or something?
  my $player = $players{$card->{player}{id}} //= {
    info  => $card->{player},
    score => 0,
  };

  my $wickets_score   = $card->{wickets} * $model->{wickets_multiplier};
  my $wickets_bonus   = threshold_bonus($card->{wickets}, $model->{wickets_bonus_multipler});
  my $maidens_score   = $card->{maidens} * $model->{maidens_multipler};
  my $dots_score      = $card->{dots} * $model->{dots_multipler};
  my $extras_score    = ($card->{wides} + $card->{noballs}) * $model->{extras_multiplier};
  my $econ_rate_bonus =
    $card->{overs} >= $model->{econ_rate_bonus_cutoff} ?
      threshold_bonus($card->{economy}, $model->{econ_rate_bonus_thresholds}, 1) : 0;

  my $innings_multiplier = $card->{inningNumber} < 3 ? $model->{first_innings_multiplier} : 1;

  $player->{score} += ($wickets_score + $wickets_bonus + $maidens_score + $dots_score + $extras_score + $econ_rate_bonus) * $innings_multiplier;

  say "BWL $card->{inningNumber} $player->{info}{name}: WS $wickets_score WB $wickets_bonus MS $maidens_score DS $dots_score ES $extras_score ERB $econ_rate_bonus";
}

say "";

# bowling, leg byes and byes

# SC penalises the bowler for these, whereas a standard scorecard doesn't even
# record them against the bowler. so we have to examine each over and look for
# the leg byes and byes directly

my @balls = 
  map { $_->{balls}->@* }
  map { $_->{stats}->@* }
  $over_data->{inningOvers}->@*;

for my $ball (@balls) {
  next unless $ball->{byes} || $ball->{legbyes};

  my $player = $players{$ball->{bowlerPlayerId}};

  my $extras_score = ($ball->{byes} + $ball->{legbyes}) * $model->{extras_multiplier};

  $player->{score} += $extras_score;

  say "BYE $ball->{inningNumber} $player->{info}{name}: ES $extras_score";
}

say "";

# fielding
for my $card (@batting_cards) {
  next unless $card->{dismissalType};

  # 1 caught
  # 2 bowled
  # 3 lbw
  # 4 run out
  # 5 stumped
  # 6 hit wicket
  # 7-11 ?
  # 12 not out

  my ($type, $mult) =
    $card->{dismissalType} == 1 ? ('C', $model->{catches_multiplier}) :   # caught
    $card->{dismissalType} == 4 ? ('R', $model->{runout_multiplier}) :    # run out
    $card->{dismissalType} == 5 ? ('S', $model->{stumping_multiplier}) :  # stumped
    ();

  next unless $type;

  my $player = $card->{dismissalFielders}->[0];
  next if $player->{isSubstitute};
  $player = $player->{player};

  $players{$player->{id}}->{score} += $mult;
  say "FLD $card->{inningNumber} $player->{name}: $type +$mult";
}

say "";

for my $player (sort { $b->{score} <=> $a->{score} } values %players) {
  say "$player->{info}{name}: $player->{score}";
};

sub threshold_bonus ($v, $thresholds, $neg = 0) {
  for my $pair (@$thresholds) {
    my ($t, $b) = @$pair;
    if ($neg) {
      return $b if $v <= $t;
    }
    else {
      return $b if $v >= $t;
    }
  }
  return 0;
}
